plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.21'
}

group = 'me.llarence'
version = '1.0-alpha'

repositories {
    mavenCentral()
}

application {
    mainModule = 'me.llarence.schcheck'
    mainClass = 'me.llarence.schcheck.MainKt'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.7.2'
    implementation 'com.calendarfx:calendar:11.12.6'
    implementation 'com.calendarfx:view:11.12.6'
    implementation 'org.slf4j:slf4j-nop:2.0.9'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(11)
}

javafx {
    version = "11"
    modules = ['javafx.controls']
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    forceMerge('slf4j', 'kotlin')
    addExtraDependencies('javafx')
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

jar {
   duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}
